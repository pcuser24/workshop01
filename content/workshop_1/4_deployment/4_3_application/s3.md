---
title : "S3"
date : "`r Sys.Date()`"
weight : 432
chapter : false
--- 

## S3 bucket
### S3 bucket
Create the S3 bucket:
```hcl
resource "aws_s3_bucket" "appbucket" {
  bucket_prefix = "${var.namespace}_appbucket"

  tags = {
    Description = "Used to store items"
  }
}
```
Restrict public access to the bucket:
```hcl
resource "aws_s3_bucket_public_access_block" "appbucket" {
  bucket = aws_s3_bucket.appbucket.bucket

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
```
### Gateway endpoint
Create a gateway endpoint to allow the EC2 instances to access the S3 bucket:
```hcl
resource "aws_vpc_endpoint" "appbucket_gateway_vpce" {
  vpc_id            = aws_vpc.ws01.id
  service_name      = "com.amazonaws.${data.aws_region.current.name}.s3"
  vpc_endpoint_type = "Gateway"
  policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Sid" : "Allow-access-to-${aws_s3_bucket.appbucket.bucket}",
        "Effect" : "Allow",
        "Principal" : "*",
        "Action" : [
          "s3:PutObject",
          "s3:DeleteObject",
          "s3:PutObjectAcl",
          "s3:PutObjectVersionAcl",
          "s3:DeleteObjectVersion",
          "s3:ListBucket",
          "s3:GetObject"
        ],
        "Resource" : [
          "${aws_s3_bucket.appbucket.arn}",
          "${aws_s3_bucket.appbucket.arn}/*"
        ]
      }
    ]
  })
  tags = {
    Name = "${var.namespace}-endpoint-s3"
  }
}
```
### Bucket policy
Create a bucket policy to allow access from the EC2 instances through Gateway endpoint:
```hcl
data "aws_iam_policy_document" "appbucket_policy" {
  statement {
    effect = "Allow"
    principals {
      type        = "*"
      identifiers = ["*"]
    }
    actions = [
      "s3:PutObject",
      "s3:DeleteObject",
      "s3:PutObjectAcl",
      "s3:PutObjectVersionAcl",
      "s3:DeleteObjectVersion",
      "s3:ListBucket",
      "s3:GetObject"
    ]
    resources = [
      aws_s3_bucket.appbucket.arn,
      "${aws_s3_bucket.appbucket.arn}/*"
    ]
    condition {
      test     = "StringEquals"
      variable = "aws:sourceVpce"
      values   = [aws_vpc_endpoint.appbucket_gateway_vpce.id]
    }
  }
}

resource "aws_s3_bucket_policy" "appbucket_policy" {
  bucket = aws_s3_bucket.appbucket.bucket
  policy = data.aws_iam_policy_document.appbucket_policy.json
}

```
### IAM policy
IAM policy for S3 bucket access from EC2 instances, which will be attached to EC2 role later on:
```hcl
resource "aws_iam_policy" "app_appbucket" {
  name        = "${var.namespace}-app-appbucket"
  description = "Allow get and put object to S3 bucket"
  policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Sid" : "AllowGetPutObject",
        "Effect" : "Allow",
        "Action" : [
          "s3:PutObject",
          "s3:DeleteObjectVersion",
          "s3:ListBucket",
          "s3:DeleteObject",
          "s3:PutObjectAcl",
          "s3:PutObjectVersionAcl",
          "s3:GetObjectAcl",
          "s3:GetObject"
        ],
        "Resource" : [
          "${aws_s3_bucket.appbucket.arn}",
          "${aws_s3_bucket.appbucket.arn}/*"
        ],
        "Resource" : "${aws_s3_bucket.appbucket.arn}/*"
      },
    ]
  })
}
```

## Cloudfront
### Cloudfront origin access identity
Make the S3 bucket as an origin for the Cloudfront distribution:
```hcl
resource "aws_cloudfront_origin_access_identity" "appbucket_oai" {
  comment = "Origin Access Identity of ${var.namespace}-appbucket"
}
```

### Cache policy
```hcl
resource "aws_cloudfront_cache_policy" "appbucket_distribution_cache_policy" {
  name        = "${var.namespace}-appbucket_distribution_cache-policy"
  comment     = "appbucket distribution cache policy"
  default_ttl = 50
  max_ttl     = 100
  min_ttl     = 1

  parameters_in_cache_key_and_forwarded_to_origin {
    cookies_config {
      cookie_behavior = "all"
    }
    headers_config {
      header_behavior = "whitelist"
      headers {
        items = ["X-Request"]
      }
    }
    query_strings_config {
      query_string_behavior = "all"
    }
  }
}
```

### Cloudfront distribution
Create a Cloudfront distribution with the S3 bucket as an origin:
```hcl
resource "aws_cloudfront_distribution" "appbucket_distribution" {
  origin {
    domain_name = aws_s3_bucket.appbucket.bucket_regional_domain_name
    origin_id   = aws_s3_bucket.appbucket.bucket

    s3_origin_config {
      origin_access_identity = aws_cloudfront_origin_access_identity.appbucket_oai.cloudfront_access_identity_path
    }
  }

  enabled             = true
  is_ipv6_enabled     = false
  comment             = "Cloudfront distribution for ${var.namespace}-appbucket"
  default_root_object = "index.html"

  default_cache_behavior {
    allowed_methods        = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods         = ["GET", "HEAD"]
    target_origin_id       = aws_s3_bucket.appbucket.bucket
    viewer_protocol_policy = "allow-all"
    compress               = true
    cache_policy_id        = aws_cloudfront_cache_policy.appbucket_distribution_cache_policy.id

    min_ttl     = 0
    default_ttl = 5 * 60
    max_ttl     = 60 * 60
  }

  price_class = "PriceClass_200"

  restrictions {
    geo_restriction {
      restriction_type = "none"
      locations        = []
    }
  }

  tags = {
    Environment = "production"
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }
}
```

## Apply changes
1. Run `terraform validate` to validate syntax
2. Run `terraform plan` to plan the deployment
3. Run `terraform apply` to apply the deployment

## Review deployment
1. Navigate to S3 console. Verify the bucket is created and public access is restricted.
![S3 bucket](/images/s3/s3_01.png)
![S3 bucket](/images/s3/s3_02.png)
![S3 bucket](/images/s3/s3_03.png)
2. Navigate to VPC console, click on Endpoint on the left pane. Verify that `ws01-endpoint-s3` is created with the correct policy.
![S3 endpoint](/images/s3/s3_endpoint.png)
3. Go to IAM console, click on Policies on the left pane. Verify that `ws01-app-appbucket` policy is created with the correct permissions.
![S3 IAM policy](/images/s3/s3_iam.png)
4. Navigate to Cloudfront console. Verify the distribution is created with the S3 bucket as an origin.
![Cloudfront origin](/images/s3/cloudfront_origin.png)
Verify the cache policy is created and attached to the distribution.
![Cloudfront cache policy](/images/s3/cloudfront_cache_policy.png)
